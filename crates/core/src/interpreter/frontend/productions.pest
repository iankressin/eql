program = _{SOI ~ (get){1, } ~ silent_eoi}

get       = {
    "GET" ~ WHITESPACE* ~ fields* ~ WHITESPACE* ~ "FROM" ~ WHITESPACE* ~ entity ~ WHITESPACE* ~ (entity_id | filter) ~ WHITESPACE* ~ "ON" ~ WHITESPACE* ~ chain ~ exp_separator* ~ WHITESPACE*
}
fields    = { (account_field_list | block_field_list | tx_field_list | log_field_list) }
entity    = { "account" | "block" | "tx" | "log" }
entity_id = { hash | account_id | block_id }
filter = _{ "WHERE" ~ WHITESPACE* ~ entity_filter}
entity_filter = { log_filter_list | block_filter_list}

// Account
account_field_list = _{ account_field ~ (", " ~ account_field)* }
account_field = {
    "nonce" |
    "balance" |
    "code"
}
account_id = _{ address | ens }

// Block
block_field_list = _{ block_field ~ (", " ~ block_field_list)* }
// TODO: Check if we need uncles
block_field      =  { 
    "hash" |
    "parent_hash" |
    "timestamp" | 
    "state_root" |
    "transactions_root" |
    "receipts_root" |
    "logs_bloom" |
    "extra_data" |
    "mix_hash" |
    "total_difficulty" |
    "base_fee_per_gas" |
    "withdrawals_root" |
    "blob_gas_used" |
    "excess_blob_gas" |
    "parent_beacon_block_root" |
    "parent_beacon_block_root" |
    "size"
}
block_id    = _{ block_tag ~ ":" ~ block_tag | block_tag }
block_tag = _{ "latest" | "earliest" | "pending" | "finalized" | "safe" | integer }
block_filter_list = _{ block_filter ~ (", " ~ block_filter)* }
block_filter = _{
    blockrange_filter
}

// Transaction
tx_field_list = _{ tx_field ~ (", " ~ tx_field)* }
tx_field = {
    "transaction_type" |
    "hash" |
    "from" | 
    "to" | 
    "data" | 
    "value" | 
    "fee" |
    "gas_price" |
    "gas" |
    "status" |
    "chain_id" |
    "v" |
    "r" |
    "s" |

    // EIP-4844
    "max_fee_per_blob_gas" |
    "blob_versioned_hashes" |

    // EIP-1559
    "max_fee_per_gas" |
    "max_priority_fee_per_gas" |

    // EIP-2930
    "access_list" |
    "y_parity"
}

// Log
log_field_list = _{ log_field ~ (", " ~ log_field)* }
log_field      =  {
    "address"
   |"topic0"
   |"topic1"
   |"topic2"
   |"topic3"
   |"data"
   |"block_hash"
   |"block_number"
   |"block_timestamp"
   |"transaction_hash"
   |"transaction_index"
   |"log_index"
   |"removed"
}
log_filter_list = _{ log_filter ~ (", " ~ log_filter)* }
log_filter = _{
    address_filter
   |topic0_filter
   |topic1_filter
   |topic2_filter
   |topic3_filter
   |blockhash_filter
   |blockrange_filter
   |event_signature_filter
}

//Filters
address_filter = {"address" ~ address}
topic0_filter = {"topic0" ~ hash}
topic1_filter = {"topic1" ~ hash}
topic2_filter = {"topic2" ~ hash}
topic3_filter = {"topic3" ~ hash}
blockhash_filter = {("blockhash" | "block_hash") ~ hash}
blockrange_filter = {"block" ~ block_id}
event_signature_filter ={"event_signature" ~ function_signature}
   
// Terminals
unit = { "ether" | "gwei" | "wei" }
number = _{ float | integer }
integer = _{ (ASCII_DIGIT)+ }
float = _{ integer ~ "." ~ integer }
chain = { "eth" | "arb" | "op" | "base" | "blast" | "polygon" | "sepolia" }
address = { "0x" ~ (ASCII_HEX_DIGIT){40} }
hash = { "0x" ~ (ASCII_HEX_DIGIT){64} }
ens = { (ASCII_ALPHANUMERIC)+ ~ ".eth" }
function_signature = { ASCII_ALPHANUMERIC+ ~ "(" ~ solidity_type* ~ ("," ~ solidity_type)* ~ ")" }
solidity_type = _{ ("uint" ~ size | "uint[]" | "uint" ) | ("bytes" ~ size | "bytes[]" | "bytes" ) | ("int" ~ size | "int[]" | "int" ) | ("address" ~ "[]"*) | "string" | "bool" }
size = _{integer ~ "[]"*}

// Helpers
WHITESPACE = _{ " " | "\t" | NEWLINE }
exp_separator = _{"," | ";"}
silent_eoi = _{ !ANY }